plugins {
    id 'java'
    id 'jacoco'
    id 'org.sonarqube' version '6.1.0.5360'
    id 'org.springframework.boot' version '3.4.4'
    id 'io.spring.dependency-management' version '1.1.4'
}

group = 'com.qa'
version = '0.0.1-SNAPSHOT'

ext {
    jacocoIncludes = [
            'com.qa.bugkeeper.service.*',
            'com.qa.bugkeeper.controller.*',
            'com.qa.bugkeeper.security.*',
    ]

    sonarIncludes = [
            '**/service/**',
            '**/controller/**',
            '**/security/**',
    ]
}

java {
    sourceCompatibility = '21'
}

jacoco {
    toolVersion = '0.8.11'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            element = 'CLASS'
            includes = jacocoIncludes
            limit {
                minimum = 0.80
            }
        }
    }
}

check.dependsOn jacocoTestCoverageVerification

sonar {
    properties {
        property 'sonar.projectKey', 'kopendozz_BugKeeper'
        property 'sonar.organization', 'kopendozz'
        property 'sonar.host.url', 'https://sonarcloud.io'
        property 'sonar.coverage.jacoco.xmlReportPaths', 'build/reports/jacoco/test/jacocoTestReport.xml'
        property 'sonar.inclusions', sonarIncludes.join(',')
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // Spring Boot Core
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'

    // Security - JWT
    implementation 'io.jsonwebtoken:jjwt-api:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.6'

    // Configuration Properties support
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    // MapStruct
    implementation 'org.mapstruct:mapstruct:1.6.3'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.3'

    // Database + Migration
    implementation 'org.postgresql:postgresql:42.7.3'
    implementation 'org.flywaydb:flyway-core:11.7.2'
    runtimeOnly 'org.flywaydb:flyway-database-postgresql:11.7.2'

    // Spring Cloud OpenFeign
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign:4.2.1'

    // Commons Utilities
    implementation 'commons-io:commons-io:2.19.0'
    implementation 'commons-fileupload:commons-fileupload:1.5'
    implementation 'org.apache.commons:commons-lang3:3.14.0'

    // Dev Tools
    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.32'
    annotationProcessor 'org.projectlombok:lombok:1.18.32'
    testCompileOnly 'org.projectlombok:lombok:1.18.32'

    //Validation
    implementation 'jakarta.validation:jakarta.validation-api:3.1.1'

    // Testing
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.assertj:assertj-core:3.27.3'
    testImplementation 'org.mockito:mockito-core:5.17.0'
    testImplementation 'com.h2database:h2:2.3.232'
    testImplementation platform('org.junit:junit-bom:5.12.2')
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}
