plugins {
    id 'java'
    id 'jacoco'
    id 'org.sonarqube' version '6.1.0.5360'
    id 'org.springframework.boot' version '3.2.5'
    id 'io.spring.dependency-management' version '1.1.4'
}

group = 'com.qa'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '21'
}

jacoco {
    toolVersion = '0.8.11'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            element = 'CLASS'
            excludes = [
                    'com.qa.bugkeeper.config.**',
                    'com.qa.bugkeeper.constant.**',
                    'com.qa.bugkeeper.entity.**',
                    'com.qa.bugkeeper.repository.**',
                    'com.qa.bugkeeper.exception.**'
            ]
            limit {
                minimum = 0.80
            }
        }
    }
}

check.dependsOn jacocoTestCoverageVerification

sonar {
    properties {
        property 'sonar.projectKey', 'kopendozz_BugKeeper'
        property 'sonar.organization', 'kopendozz'
        property 'sonar.host.url', 'https://sonarcloud.io'
        property 'sonar.coverage.jacoco.xmlReportPaths', 'build/reports/jacoco/test/jacocoTestReport.xml'
        property 'sonar.exclusions', '**/config/**, **/constant/**, **/exception/**, **/repository/**, **/entity/**'
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign:4.1.1'
    implementation 'org.postgresql:postgresql:42.7.3'
    implementation 'org.flywaydb:flyway-core:10.11.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.0'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.17.0'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.17.0'
    implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity6:3.1.1.RELEASE'
    implementation 'commons-io:commons-io:2.11.0'
    implementation 'commons-fileupload:commons-fileupload:1.5'
    implementation 'org.apache.commons:commons-lang3:3.14.0'
    implementation 'org.webjars:bootstrap:3.4.1'
    implementation 'org.webjars:jquery:1.12.4'
    implementation 'org.webjars:jquery-validation:1.20.0'
    implementation 'org.webjars:jquery-ui:1.13.2'
    implementation 'org.webjars:datatables:1.13.5'
    implementation 'org.webjars:pure:0.6.2'
    implementation 'org.webjars.npm:tokenize2:1.3.0'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    compileOnly 'org.projectlombok:lombok:1.18.32'
    runtimeOnly 'org.flywaydb:flyway-database-postgresql:10.11.0'
    annotationProcessor 'org.projectlombok:lombok:1.18.32'
    testCompileOnly 'org.projectlombok:lombok:1.18.32'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.assertj:assertj-core:3.27.3'

    testImplementation platform('org.junit:junit-bom:5.12.2')
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testRuntimeOnly  'org.junit.jupiter:junit-jupiter-engine'
    testRuntimeOnly  'org.junit.platform:junit-platform-launcher'

    testAnnotationProcessor 'org.projectlombok:lombok:1.18.32'
}
